buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
			name = 'SpongePowered'
			url = 'https://repo.spongepowered.org/repository/maven-public/'
		}
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:4.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
		classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
 
 
version = "0.1-beta7"
group = "me.creeper.creeperclient" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "creeperClient"
 
sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
 
minecraft {
    //version = "1.12.2-14.23.5.2855"
    //runDir = "run"
	runs {
		client {
			workingDirectory project.file('run')
		}
	}
 
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
	mappings channel: "stable", version: "39-1.12"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}
 
repositories {
	maven {
		name = 'spongepowered-repo'
		url = 'https://repo.spongepowered.org/repository/maven-public/'
	}
	maven {
		name = 'swt-repo'
		url = "http://maven-eclipse.github.io/maven"
	}
	maven {
		name = "jitpack.io"
		url = "https://jitpack.io"
	}
	maven {
		name = 'lukflug'
		url = 'https://lukflug.github.io/maven'
	}
	mavenCentral()
}
 
dependencies {
	minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'

	compile("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
		exclude module: 'launchwrapper'
		exclude module: 'guava'
		exclude module: 'gson'
		exclude module: 'commons-io'
		exclude module: 'log4j-core'
	}
	annotationProcessor('org.spongepowered:mixin:0.8.2:processor') {
		exclude module: 'gson'
	}
	compile('com.lukflug:panelstudio:0.1.8')
	compile('com.lukflug:panelstudio-mc12:0.1.8')
	compile "com.github.ZeroMemes:Alpine:1.5"
	compile('net.jodah:typetools:0.6.3')
	compile('com.googlecode.json-simple:json-simple:1.1.1')
	compile('club.minnced:java-discord-rpc:2.0.1')
}
 
processResources {
	inputs.property 'version', project.version
 
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
	rename '(.+_at.cfg)', 'META-INF/$1'
}
 
mixin {
	defaultObfuscationEnv 'searge'
	add sourceSets.main, 'mixins.creeper.refmap.json'
}
 
reobf {
	shadowJar {
		//mappingType = 'SEARGE'
		classpath = sourceSets.main.compileClasspath
	}
}

task sourceJar(type: Jar) {
	enabled false;
}
 
jar {
	manifest {
		attributes([
				'MixinConfigs': 'mixins.creeper.json',
				'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
				'TweakOrder': 0,
				'FMLCorePluginContainsFMLMod': 'true',
				//'FMLCorePlugin': 'me.spruce.creeperclient.mixin.MixinLoader',
				'ForceLoadAsMod': 'true',
				'FMLAT': '***_at.cfg'
		])
	}
}
 
shadowJar {
	dependencies {
		include(dependency('org.spongepowered:mixin'))
		include(dependency('com.github.ZeroMemes:Alpine'))
		include(dependency('net.jodah:typetools'))
		include(dependency('com.googlecode.json-simple:json-simple:1.1.1'))
		include(dependency('club.minnced:java-discord-rpc:2.0.1'))
		include(dependency('com.lukflug:panelstudio'))
		include(dependency('com.lukflug:panelstudio-mc12'))
	}
	exclude 'dummyThing'
	exclude 'LICENSE.txt'
}
